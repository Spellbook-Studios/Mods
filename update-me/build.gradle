plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name 'Xander Maven'
        url 'https://maven.isxander.dev/releases'
    }
    maven { // Used fpr gson used in yacl
        url "https://maven.quiltmc.org/repository/release"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    modImplementation ("dev.isxander.yacl:yet-another-config-lib-fabric:${yacl_version}") {
        //exclude(group: "org.quiltmc.parsers")
        exclude(group: "com.twelvemonkeys.common")
        exclude(group: "com.twelvemonkeys.imageio")
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") // Remember to have the MODRINTH_TOKEN environment variable set or else this will fail - just make sure it stays private!
    projectId = "9xIIYjBA" // This can be the project ID or the slug. Either will work!
    versionNumber = project.mod_version // You don't need to set this manually. Will fail if Modrinth has this version already
    versionName = "Update Me " + versionNumber
    versionType = "release" // This is the default -- can also be `beta` or `alpha`
    uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = [project.minecraft_version] // Must be an array, even with only one version
    changelog = project.hasProperty('changeLog') ?
            project.getProperty('changeLog') : null
    loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
    syncBodyFrom = project.file("README.md").text
    dependencies {
        required.project "yacl" // Creates a new required dependency on Fabric API
    }
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)